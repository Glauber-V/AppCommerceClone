plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

android {
    namespace 'com.example.appcommerceclone'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.appcommerceclone"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }

    lint {
        baseline file('lint-baseline.xml')
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    packaging.resources {
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {

    def composeBom = platform("androidx.compose:compose-bom:2023.05.01")

    // -- AndroidX
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"

    // -- Material Components
    implementation "com.google.android.material:material:1.9.0"

    // -- Compose
    implementation composeBom
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"

    // -- Architecture
    implementation "androidx.activity:activity-compose:1.7.2"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"

    implementation "androidx.navigation:navigation-compose:2.6.0"

    // -- Hilt Dependency Injection
    implementation "com.google.dagger:hilt-android:2.46.1"
    kapt "com.google.dagger:hilt-compiler:2.46.1"

    // -- Java 8+ API desugar support
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.3"

    // -- Moshi - Convert JSON to Kotlin object
    implementation "com.squareup.moshi:moshi:1.13.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

    // -- Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // -- OkHttp
    implementation "com.squareup.okhttp3:okhttp:4.11.0"

    // -- Coil - Image Loading
    implementation "io.coil-kt:coil:2.4.0"
    implementation "io.coil-kt:coil-compose:2.4.0"

    // -- Dependencies for unit tests
    testImplementation "junit:junit:4.13.2"

    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "androidx.test.ext:junit-ktx:1.1.5"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "androidx.navigation:navigation-testing:2.6.0"

    testImplementation "androidx.test.espresso:espresso-core:3.5.1"
    testImplementation "androidx.test.espresso:espresso-contrib:3.5.1"

    testImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"

    testImplementation "com.google.truth:truth:1.1.2"

    testImplementation "com.google.dagger:hilt-android-testing:2.46.1"
    kaptTest "com.google.dagger:hilt-android-compiler:2.46.1"

    testImplementation "org.robolectric:robolectric:4.10"
}